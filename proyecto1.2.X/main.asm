
;*******************************************************************************
;    Filename:   Alarma
;    Date:	    08/08/2020
;    File Version: 1V
;    Author:   Angel Orellana
;    Company:   UVG
;    Description:   Proyecto 1 
;*******************************************************************************
#include "p16f887.inc"

; CONFIG1
; __config 0xE0D4
 __CONFIG _CONFIG1, _FOSC_INTRC_NOCLKOUT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_OFF & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF
; CONFIG2
; __config 0xFFFF
 __CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF

; TODO INSERT CONFIG HERE

;******************************************************************************

; TODO PLACE VARIABLE DEFINITIONS GO HERE
  
VALOR_CUENTATMR0 EQU  .250 ;250
VALOR_LED EQU .63   ;63
VALOR_TMR1L EQU .220 ;220
 VALOR_TMR1H EQU .11 ; 11 ENTRE LOS DOS REGISTROS HACEN 3036 VALOR INICIAR DEL TMR1
 VALOR_TMR0 EQU	.6 ;.6 
VALOR_TMR1  EQU .2  ;2 VECES QUE SE TIENE QUE ENTRAR EN TMR1_INT 
 VALOR_TITILAR_7S EQU .125 ;63
 VALOR_BUZZER EQU .125 ;125
 VALOR_TMR2 EQU .125 ;125
 VALOR_CONTADOR_TMR2 EQU .50 ;50
VALOR_CONTADOR_ALARMA EQU .250
 VALOR_SEGUNDOS_ALARMA EQU .60
 
DATOS	UDATA
;------------   CONTADORES PARA EVENTOS -------------------------------------------------------------------------------------
CUENTATMR0	RES 1 ;CUENTA CUANTAS VECES SE HA ACTIVADO LA INTERRUPCION DEL TMR 0
CUENTALED   RES	1 ;USADA PARA REALIZAR EL ENCENDIDO Y APAGADO DEL LED
CONTADOR_TMR1 RES 1 ;USADO PARA GATE TMR1 
TITILAR_DISPLAYS RES 1 ;CONTADOR USADO PARA HACER TITILAR LOS DISPLAYS
CONTADOR_BUZZER RES 1 ;CONTADOR USADO PARA HACER SONAR EL BUZZER
CONTADOR_TMR2 RES 1 ;USADO PARA CONTAR EL NUMERO DE VECES QUE SE ENTRA EN TMR2_INT CUANDO
CONTADOR_ALARMA RES 1 ;USADO PARA CONTAR 1 SEGUNDO DESDE QUE SE ACTIVO LA ALARMA
SEGUNDOS_ALARMA RES 1 ;USADOR PARA CONTAR 1 MINUTO DESDE QUE SE ACTIVO LA ALARMA
 DELAY	RES 1 ;USADO PARA CREAR DELAY
	
 ;--------------------------------CONTROL DE EJECUCION --------------------------------------------------------------------------
CONTROL_DISPLAY RES 1 ;DISPLAY QUE SERA ENCENDIDO
 FLAGS RES 1 ;BANDERAS QUE INDICAN QUE ALGUNA CONDICION SE CUMPLIO
	    ; BIT 0, CAMBIO DE DIA 
	    ;BIT 1, SE ESTA EN MODO  EDICION, 
	    ;BIT 2 SI SE EDITA MINUTO/MES (O) O SI SE EDITA MES/DIA(1)
	    ; BIT 3 ES PARA VERIFICAR SI  YA SE ENTRO EN EL MODO EDICION ANTERIORMENTE
	    ;BIT 4 ES PARA CONTROLAR LA TITILACION DE LOS DISPLAYS
	    ;BIT 5 CONTROLAR SI SE DEBE SONAR EL BUZZER PARA AL GUARDAR UN DATO
	    ;BIT 6 CONTROLA SI SUENA LA ALARMA DEL TEMPORIZADOR
FLAGS_2	RES 1 ;BANDERAS PARA INDICAR QUE ALGUNA CONDICION SE CUMPLIO
	 ;BIT 0, FUE NECESARIO REALIZAR CORRECCION BCD AL DISMINUIR
	 ;BIT 1, FUE NECESARIO REALIZAR CORRECCION BCD AL AUMENTAR
OPCION RES 1 ;SELECCION DEL MODO DE OPERACION
PORTB_ACTUAL RES 1 ;VALOR A GUARDAR EL VALOR DEL PUERTO B 
PORTB_ANTERIOR RES 1; VALOR ANTERIOR DEL PUERTO B
 
;---------------------------------------MODO DE RELOJ --------------------------------------------------------------------------------- 
SEGUNDOS    RES	    1
MINUTOS	    RES	1  ;GUARDA EL VALOR DE LOS MINUTOS EN BCD
HORAS	RES 1	;GUARDA LA HORA EN BCD
	
;----------------------------------MODO DE RELOJ EDICION---------------------------------------------------------------------------
MINUTO_EDITAR RES 1 ;USADA PARA GUARDAR EL VALOR A ESTABLECER AL EDITAR MINUTO
HORA_EDITAR  RES 1 ;USADA PARA GUARDAR EL VALOR A ESTABLECER AL EDITAR HORA
  
;-----------------------------------------MODO FECHA ------------------------------------------------------------------------------------
MES_BCD RES 1; VALOR DEL MES EN BCD
MES_BIN RES 1 ;VALOR DEL MES EN BINARIO
DIA RES 1   ;VALOR DEL DIA EN BCD
DIA_MAX RES 1 ;DIA MAXIMO POSIBLE
 
;-----------------------------------------MODO FECHA EDICION------------------------------------------------------------------------
MES_BCD_EDITAR RES 1  ;USADA PARA GUARDAR EL VALOR A ESTABLECER AL EDITAR LA HORA EN BCD
MES_BIN_EDITAR RES 1 ; ;USADA PARA GUARDAR EL VALOR A ESTABLECER AL EDITAR LA HORA EN BINARIO
DIA_EDITAR RES 1  ;USADA PARA GUARDAR EL VALOR A ESTABLECER AL EDITAR EL DIA
 
;-----------------------------------------MODO TEMPORIZADOR------------------------------------------------------------------------
SEGUNDOS_TEMPORIZADOR RES 1 ;SEGUNDOS EN EL TEMPORIZADOR
MINUTOS_TEMPORIZADOR RES 1 ;MINUTOS EN EL TEMPORIZADOR
 
;-----------------------------------------MODO TEMPORIZADOR EDICION------------------------------------------------------------
SEGUNDOS_TEMPORIZADOR_EDITAR RES 1 ;SEGUNDOS EN EL TEMPORIZADOR A EDITAR
MINUTOS_TEMPORIZADOR_EDITAR RES 1 ;MINUTOS EN EL TEMPORIZADOR A EDITAR
 
;-----------------------------------------ALMACENAMIENTO TEMPORAL DE DATOS------------------------------------------------
 SAVE_TEMP RES 1  ; SE USA PARA GUARDAR TEMPORALMENTE EL VALOR ESCRITO EN ALGUN PUERTO
 WRAM RES 1 ; GUARDAR EL VALOR DE W AL ENTRAR A UNA INTERRUPCION
 STATUSRAM RES 1 ;GUARDAR EL VALOR DE STATUS AL ENTRAR EN UNA INTERRUPCION
 ;---------------------------------------- VARIABLES COMO PARAMETROS DE FUNCIONES --------------------------------------
 DISPLAY_1 RES 1 ;USADA PARA ALMACENAR EL VALOR EN EL DISPLAY 1 Y 2 
 DISPLAY_2  RES 1;USADA PARA ALMACENAR EL VALOR EN EL DISPLAY 3 Y 4
 ;*******************************************************************************
; Reset Vector
;*******************************************************************************

RES_VECT  CODE    0x0000            ; processor reset vector
    GOTO    START                   ; go to beginning of program

INTERRUPT_VEC CODE 0x0004
  SAVE:
    MOVWF WRAM
    SWAPF   STATUS,W
    MOVWF   STATUSRAM
    SELECCIONAR
    BTFSC   INTCON,2 ; SI ESTA ACTIVA SE EJECUTA LO SIGUIENTE
    GOTO TMR0_INT
    BTFSC PIR1, 0 ;OVERFLOW EN EL TIMER 1
    GOTO    TMR1_INT
    BTFSC   PIR1,1 ;OVERFLOW EN EL TIMER 2
    GOTO TMR2_INT
    
    GOTO LOAD
TMR0_INT:
    MOVLW   VALOR_TMR0
    MOVWF   TMR0
    BCF	INTCON,2
    
    ;---------------------------- CUENTA DE LA ALARMA -----------------------------------------------
    BTFSS   FLAGS,6 ;SI LA ALARMA ESTA ACTIVA SE REALIZA LA CUENTA 
    GOTO SONAR_BUZZER
    
    DECFSZ  CONTADOR_ALARMA
    GOTO    SONAR_BUZZER
    
    MOVLW   VALOR_CONTADOR_ALARMA
    MOVWF   CONTADOR_ALARMA
   DECFSZ   SEGUNDOS_ALARMA,F 
   GOTO SONAR_BUZZER
   
   BCF	FLAGS,6
   MOVLW    VALOR_SEGUNDOS_ALARMA
   MOVWF    SEGUNDOS_ALARMA
    ;----------------------- SE HACE SONAR EL BUZZER ----------------------------------------------
    SONAR_BUZZER
    BTFSS   FLAGS,5 ;SI SE ACTIVA ESTE FLAG SE HACE SONAR EL BUZZER
    GOTO    TITILAR_7S
    
    BSF PORTE,1
    DECF    CONTADOR_BUZZER,F
    BTFSS   STATUS,2 ;SI DA CERO LO ANTERIOR SE SALTA
    GOTO    TITILAR_7S
    
    MOVLW   VALOR_BUZZER
    MOVWF   CONTADOR_BUZZER
    BCF	PORTE,1
    BCF	FLAGS,5
    ;--------------------- SE HACE TITILAR LOS DISPLAYS ------------------------------------------
    TITILAR_7S
    DECFSZ    TITILAR_DISPLAYS,F ;SI LO ANTERIOR DA 0 SE SALTA EL GOTO
    GOTO    PUNTOS_ENMEDIO
    
    MOVLW   VALOR_TITILAR_7S
    MOVWF   TITILAR_DISPLAYS
    
    MOVF    FLAGS,W
    ANDLW   B'11101111' ;SE CONSERVAN TODOS MENOS EL BIT 4 
    MOVWF   SAVE_TEMP
    COMF	FLAGS, W
    ANDLW   B'0010000' ;SE CONSERVA SOLO EL BIT4 
    ADDWF   SAVE_TEMP,W
    MOVWF   FLAGS
    
;-------------------------------------- TITILAR LOS PUNTOS DE EN MEDIO EN MODO NORMAL ----------------------------
    PUNTOS_ENMEDIO
    DECFSZ    CUENTALED,F ;SI DA 0 ES QUE YA PASARON 500 MS
    GOTO    CONTAR_SEGUNDO
    
	
    ;--------------------- SE HACE TITILAR EL LED -----------------------------------------------------
    MOVF    PORTE,W
    ;----- SI LA ALARMA DEL TEMPORIZADOR ESTA ACTIVA SE CAMBIA EL RE1 TAMBIEN
    BTFSS   FLAGS,6    
    ANDLW   B'1011' ;SE CONSERVAN TODOS MENOS RE2 
    BTFSC   FLAGS,6 
    ANDLW   B'1001'
   ;-------CONTINUA NORMAL----------
    MOVWF   SAVE_TEMP
    COMF	PORTE, W
    BTFSS   FLAGS,6
    ANDLW   B'0100' ;SE CONSERVA SOLO RE2 
    BTFSC   FLAGS,6
    ANDLW   B'0110' ;SE CONSERVA SOLO RE2 Y RE1 PARA HACER SONAR LA ALARMA DEL TEMPORIZADOR
    ADDWF   SAVE_TEMP,W
    MOVWF   PORTE
    MOVLW   VALOR_LED
    MOVWF   CUENTALED
    
    ;------------------- SE REGISTRA LA CUENTA DE CADA SEGUNDO ---------------------------------
    CONTAR_SEGUNDO
    DECFSZ	CUENTATMR0  ;SI LO ANTERIOR DA 0 SE EJECUTA EL RESTO
    
    GOTO LOAD
    
 
    MOVLW  VALOR_CUENTATMR0
    MOVWF   CUENTATMR0
    INCF    SEGUNDOS,F
    GOTO LOAD
    
TMR1_INT:
    MOVLW   VALOR_TMR1H
    MOVWF   TMR1H
    MOVLW   VALOR_TMR1L
    MOVWF   TMR1L
    BCF	PIR1,0 ;SE LIMPIA EL FLAG DE LA INTERRUPCION
    
    DECF CONTADOR_TMR1
    BTFSS   STATUS,2 ;SI LO ANTERIOR DA 0 SE EJECUTA EL CODIGO QUE LE SIGUE
    GOTO    LOAD
    
    MOVLW  VALOR_TMR1
    MOVWF   CONTADOR_TMR1
    BCF	T1CON,0 ;SE APAGA EL TMR1
    BCF	FLAGS,1 ;SE SALE DEL MODO EDICION
    BCF	FLAGS,2 ;SE COLOCA EN DIA/HORA EN LA EDICION
    BSF	FLAGS,5 ;SE ACTIVA QUE SUENE EL BUZZER
    
    BTFSC   OPCION,0 ;SI ESTABA ACTIVO ES QUE SE ESTABA EDITANDO LA HORA
    GOTO    EDITANDO_HORA
    BTFSC   OPCION,1 ;SE ESTABA EN 1, ES QUE SE ESTABA EDITANDO LA FECHA
    GOTO    EDITANDO_FECHA
    BTFSC   OPCION,2 ;SI ESTABA EN 1, ES QUE SE ESTABA EDITANDO EL CRONOMETRO
    GOTO    EDITANDO_TIMER
    
    EDITANDO_HORA; SE ESTABA EDITANDO LA HORA Y SE GUARDAN LOS VALORES ESTABLECIDOS
    MOVF    MINUTO_EDITAR,W
    MOVWF   MINUTOS
    MOVF    HORA_EDITAR,W
    MOVWF   HORAS
    CLRF    SEGUNDOS
    GOTO LOAD
    
    EDITANDO_FECHA; SE ESTABA EDITANDO LA FECHA Y SE GUARDAN LOS VALORES ESTABLECIDOS
    MOVF    MES_BCD_EDITAR,W
    MOVWF   MES_BCD
    MOVF    DIA_EDITAR,W
    MOVWF   DIA
    MOVF    MES_BIN_EDITAR,W
    MOVWF   MES_BIN
    GOTO LOAD
    
    EDITANDO_TIMER
    BSF	T2CON,2 ;SE ENCIENDE EL TEMPORIZADOR
    MOVF    SEGUNDOS_TEMPORIZADOR_EDITAR,W
    MOVWF   SEGUNDOS_TEMPORIZADOR
    MOVF   MINUTOS_TEMPORIZADOR_EDITAR,W
    MOVWF   MINUTOS_TEMPORIZADOR
    
TMR2_INT:
    BCF	PIR1,1 ;SE LIMPIA
    DECF    CONTADOR_TMR2,F
    BTFSS   STATUS,2
    GOTO    LOAD
    
    MOVLW   VALOR_CONTADOR_TMR2
    MOVWF   CONTADOR_TMR2
    DECFSZ    SEGUNDOS_TEMPORIZADOR,F
    GOTO    FUNCIONAMIENTO_NORMAL_TEMPORIZADOR
      ;--------- SE VERIFICA SE SE AGOTO EL TIEMPO ----------------
    MOVLW   .0
    SUBWF   MINUTOS_TEMPORIZADOR,W ;LA UNICA FORMA DE QUE ESTO DE 0 ES SI MINUTOS ES 0
    BTFSS   STATUS,2 ;1 SI LO ANTERIOR DA 0
    GOTO  FUNCIONAMIENTO_NORMAL_TEMPORIZADOR
    ;SI ES CERO ESTO PASA
    BSF	FLAGS,6 ;SUENA LA ALARMA DEL TEMPORIZADOR
    BCF	T2CON,2 ; SE APAGA EL TMR2
    CLRF	TMR2 ;SE LIMPIA EL REGISTRO DEL TMR2
    CLRF    SEGUNDOS_TEMPORIZADOR
    GOTO LOAD
    
    FUNCIONAMIENTO_NORMAL_TEMPORIZADOR
    ;--- SE VERIFICA SI PASA DE 00 A 59
    MOVLW .255 
    SUBWF   SEGUNDOS_TEMPORIZADOR,W
    BTFSS   STATUS,2 ; SI DA 0 ES QUE SI SE PASO DE 00 A 59
    GOTO LOAD
    
    MOVLW B'01011001' ;59 EN BCD
    MOVWF   SEGUNDOS_TEMPORIZADOR
  
    
    
    
    DECREMENTAR_MINUTOS_TEMPORIZADOR
    ;------- SI NO SE ACABO EL TIEMPO DE DECREMENTAN LOS MINUTOS
    DECF    MINUTOS_TEMPORIZADOR 
    
    ;LA CORRECCION DE LA CODIFICACION EN BCD SE SE REALIZA EN EN LOOP PRINCIPAL
    
    
LOAD:
    SWAPF STATUSRAM,W
    MOVWF   STATUS
    SWAPF   WRAM,F
    SWAPF   WRAM,W
    RETFIE

    ;----------------------- TABLAS ------------------------------------------------------------------------------------------
 TABLA:
    ADDWF   PCL,F
    RETLW B'01110111'  ; 0  EN EL DISPLAY
    RETLW B'01000001' ;1
    RETLW B'00111011' ;2
    RETLW B'01101011';3
    RETLW B'01001101';4
    RETLW   B'01101110' ;5
    RETLW B'01111110' ;6
    RETLW   B'01000011' ;7
    RETLW   B'01111111';8
    RETLW   B'01101111';9
    
TABLA_MESES:
    BTFSC   FLAGS, 1; SI SE ESTA EN MODO EDICION SE  PASA ESTE VALOR 
    MOVF    MES_BIN_EDITAR,W
    BTFSS   FLAGS,1 ; SI SE ESTA EN MODO VISUALIZACION SE PASA ESTE VALOR 
    MOVF    MES_BIN,W
    ADDWF   PCL,F
    NOP ; VARIABLE PARA 0 (EN FUNCIONAMIENTO NORMAL NO DEBERIA EJECUTARSE)
    RETLW   B'00110010'  ;32 EN BCD  ENERO
    RETLW   B'00101001' ;29 EN BCD FEBRERO
    RETLW   B'00110010'  ;32 EN BCD  MARZO
    RETLW   B'00110001' ;31 EN BCD ABRIL
    RETLW   B'00110010'  ;32 EN BCD  MAYO
    RETLW   B'00110001' ;31 EN BCD JUNIO
    RETLW   B'00110010'  ;32 EN BCD  JULIO
    RETLW   B'00110010'  ;32 EN BCD  AGOSTO
    RETLW   B'00110001' ;31 EN BCD SEPTIEMBRE
    RETLW   B'00110010'  ;32 EN BCD  OCTUBRE
    RETLW   B'00110001' ;31 EN BCD NOVIEMBRE
    RETLW   B'00110010'  ;32 EN BCD  DICIEMBRE    
    
;*******************************************************************************
; MAIN PROGRAM
;*******************************************************************************

MAIN_PROG CODE                      ; let linker place main program

START:
    BSF	STATUS,6
    BSF	STATUS,5 ;BANCO 3
    
    CLRF    ANSEL
    CLRF    ANSELH ;PUERTOS A, E Y B COMO I/O DIGITALES
    
    
    BCF	STATUS,6; BANCO 1 

    
    MOVLW   .255
    MOVWF   TRISB ;PUERTO B COMO ENTRADA DIGITAL
    CLRF   TRISA ;PUERTO A COMO SALIDA
    CLRF    TRISE ;PUERTO E COMO SALIDA
    CLRF    TRISC  ;PUERTO C, PARA EL DISPLAY
    CLRF    TRISD ;PUERTO D, PARA CONTROLAR LOS DISPLAYS
    
    MOVLW   B'00000011' ;PULL UP ACTIVAS, PRESCALER DE 1:16 ASIGNADO AL TMR0 
    MOVWF   OPTION_REG ;
    MOVLW   B'0000011' ;TMR2 Y TMR1 INTERUPCCION ACTIVADA
    MOVWF   PIE1
    
    MOVLW   VALOR_TMR2
    MOVWF   PR2 ;CUENTA 125 VECES SE EJECUTA LA INTERRUPCION CADA 20mS
    
    MOVLW   B'11100000' ;INTERRUPCIONES ACTIVADAS, TMR0 INTERRUPCION ACTIVADA, PORTB INT ACTIVADA
    MOVWF   INTCON
    
    
    BCF	STATUS,5 ;BANCO 0
    
    MOVLW   B'01100000' ;APAGADO, GATE MODE ES ACTIVE LOW, Y CON PREESCALER DE 1:4
    MOVWF   T1CON ;SE USARA PARA EL PARPADEO DE LOS DISPLAY CUANDO SE ESTE EN MODO DE EDICION
    MOVLW B'1001010'
    MOVWF   T2CON ;PRESSCALER 16 POST ESCALER DE 10 SE USA PARA EL CRONOMETRO OVERFLOW CADA 20mS
    
    CLRF  PORTD
    CLRF    PORTC
    CLRF    PORTE
    MOVLW  VALOR_TMR0;CUENTA DEL TMR0 INICIA EN 131 PARA QUE LA INTERRUPCION SE EJECUTE CADA
    MOVWF   TMR0 ;4 ms 
    MOVLW   VALOR_TMR1H
    MOVWF   TMR1H
    MOVLW   VALOR_TMR1L
    MOVWF   TMR1L
    
    MOVLW   VALOR_LED
    MOVWF   CUENTALED
    MOVLW  VALOR_CUENTATMR0
   MOVWF    CUENTATMR0
    CLRF	SEGUNDOS
    CLRF    MINUTOS
    CLRF    HORAS
    CLRF    FLAGS
    CLRF    MINUTOS_TEMPORIZADOR
    CLRF    SEGUNDOS_TEMPORIZADOR
    MOVLW   .255
    MOVWF	   PORTB_ACTUAL
    MOVWF    PORTB_ANTERIOR
    
    MOVLW   VALOR_TMR1
    MOVWF   CONTADOR_TMR1
    
    MOVLW   B'00000001'
    MOVWF   CONTROL_DISPLAY
    MOVWF   OPCION
   MOVWF   DIA
    MOVWF   MES_BCD
    MOVWF   MES_BIN
    MOVLW   VALOR_BUZZER
    MOVWF   CONTADOR_BUZZER
    MOVLW   VALOR_CONTADOR_TMR2
    MOVWF   CONTADOR_TMR2
    MOVWF   PORTA
    
    
   MOVLW    VALOR_SEGUNDOS_ALARMA
   MOVWF    SEGUNDOS_ALARMA
    MOVLW   VALOR_CONTADOR_ALARMA
    MOVWF   CONTADOR_ALARMA
LOOP:
    ;-------------------------------------- CONTROL DE LA HORA Y FECHA DEL RELOJ -------------------------------------------
    MOVLW .60
    SUBWF   SEGUNDOS,W
    BTFSC	STATUS,2    ;SI SE CONTO 1 SEGUNDO SE EJECUTA 
    CALL VALOR_RELOJ
    BTFSC   FLAGS,0 ; SI SE ACTIVA LA BANDERA DE FECHA ENTONCES SE CAMBIA LA FECHA
    CALL VALOR_FECHA
    ;-------------------------------------- CONTROL DEL TEMPORIZADOR -----------------------------------------------------------
    BTFSC  T2CON,2 ;SE VERIFICA QUE EL TEMPORIZADOR ESTE EN FUNCIONAMIENTO
    CALL    VALOR_TEMPORIZADOR
    ;-------------------------------------------------------------------------------------------------------------------------------------------
        ; ----------CONTROL DE LA PRSISTENCIA DE VISION PARA LOS DISPLAYS---------------------------------------
    ADDLW   .0 ;SE ASEGURA QUE EL BIT CARRY DE STATUS ESTE EN 0 PARA QUE EL CORRIEMENTO DE BIS SEA 
    BTFSS	CONTROL_DISPLAY,3
    GOTO    NORMAL
   
    CLRF    CONTROL_DISPLAY
    MOVLW   .255
    ADDLW   .1

    NORMAL
    RLF	CONTROL_DISPLAY ;SE CORREN 1 ESPACIO A LA IZQUIERDA LOS BITS
   ;-----------------------------------------------------------------------------------------------------------------------------------------
    ;---------------------------------------- LECTURA DEL PUERTOB ----------------------------------------------------------------
    ;SI HAY UN FLANCO DE SUBIDA EN CUALQUIERA DE LOS PUERTOS ENTONCES SE CAMBIA DE OPCION
    MOVF    PORTB_ACTUAL,W
    MOVWF   PORTB_ANTERIOR
    CALL DELAY_US ;SE ESPERA UN MOMENTO
    MOVF    PORTB,W
    MOVWF   PORTB_ACTUAL
    ;------------------------ SE VERIFICA SI SE PRESIONO EL BOTON PARA ENTRAR EN MODO EDICION------------
    BTFSC   PORTB_ANTERIOR,5
   GOTO    ESTABLECER_OPCION
    
    BTFSC   PORTB_ACTUAL,5
    BSF	FLAGS,1
    
    
    ;------------------------SE VERIFICA SI SE PRESIONO EL BOTON PARA CAMBIAR DE OPCION ---------------------
    ESTABLECER_OPCION
    ;----------------------SE VERIFICA QUE NO SI SE ENCUENTRA EN EL MODO EDICION------------------------------
    BTFSC   FLAGS,1
    GOTO OPCIONES ;SI SE ENCUENTRA EN ESTE MODO NO SE ATIENDE A RB2 PARA EL CAMBIO DE MODO
    ;----------------------------------------------------------------------------------------------------------------------------------------
    ;----------------------------------- SI SE ENCUENTRA EN MODO VISUALIZACION ---------------------------------------
    ;-------FLANCO DE SUBIDA EN RB2 -------
    BTFSC   PORTB_ANTERIOR,2 ;SI EL ANTERIOR ES 1 SE VERIFICA QUE EL VALOR ACTUAL SEA 0
    GOTO OPCIONES
    
    BTFSS   PORTB_ACTUAL,2 ;SI ES 0 SE CAMBIA DE OPCION
    GOTO OPCIONES
    ;----------------------------------------------------
   ;------------- SE LLEGA A AQUI SI SE DETECTA UN FLANCO DE SUBIDA------------------------------------------------
    BTFSS   OPCION,2 ;SI ES 0 ES QUE TODAVIA NO SE HA ENTRADO A LA OPCION 3
    GOTO OPCIONES_NORMAL
    
    MOVLW .1
    MOVWF   OPCION
    GOTO OPCIONES
    
    OPCIONES_NORMAL
    MOVLW .0
    ADDLW   .0 ;SE ASEGURA QUE EL BIT C DE STATUS ESTE EN 0
    RLF	OPCION	
   ;-----------------------------------------------------------------------------------------------------------------------------------------
OPCIONES:
    MOVF    OPCION,W
    MOVWF   PORTA ;SE VISUALIZA QUE OPCION ESTA SELECCIONADA
    BTFSC   OPCION,0
    GOTO OPCION_RELOJ
    BTFSC  OPCION,1
    GOTO OPCION_FECHA
    BTFSC   OPCION,2
    GOTO OPCION_TEMPORIZADOR
    GOTO LOOP
    
    OPCION_RELOJ:
    
    ;--------------- SE VERIFICA SI SE ENCUENTRA EN MODO EDITAR O NO-----------------------------------------
    BTFSS   FLAGS,1
    GOTO NO_EDITAR_HORA
    GOTO SI_EDITAR_HORA
    
    NO_EDITAR_HORA:
    MOVF    MINUTOS,W
    MOVWF   DISPLAY_1
    MOVF    HORAS,W
    MOVWF   DISPLAY_2
    CALL DISPLAY_NO_EDITAR
   
    GOTO    LOOP
    
    SI_EDITAR_HORA:
    CALL    EDITAR_HORA
    MOVF    MINUTO_EDITAR,W
    MOVWF   DISPLAY_1
    MOVF    HORA_EDITAR,W
    MOVWF   DISPLAY_2
    CALL DISPLAY_EDITAR
    GOTO LOOP
    
    OPCION_FECHA:
    BTFSS   FLAGS,1
    GOTO NO_EDITAR_FECHA
    GOTO SI_EDITAR_FECHA
    
    NO_EDITAR_FECHA:
    MOVF    DIA,W
    MOVWF   DISPLAY_2
    MOVF    MES_BCD,W
    MOVWF   DISPLAY_1
     CALL DISPLAY_NO_EDITAR
    
     GOTO LOOP
    
    SI_EDITAR_FECHA:
    CALL EDITAR_FECHA
    MOVF    MES_BCD_EDITAR,W
    MOVWF   DISPLAY_1
    MOVF    DIA_EDITAR,W
    MOVWF   DISPLAY_2
    CALL DISPLAY_EDITAR
    GOTO LOOP
    
    OPCION_TEMPORIZADOR:
    BTFSS FLAGS,1
    GOTO    NO_EDITAR_TEMPORIZADOR
    GOTO    SI_EDITAR_TEMPORIZADOR
    
    NO_EDITAR_TEMPORIZADOR:
    BTFSC PORTB_ANTERIOR,1 ;SI SE HACE UN FLANCO DE SUBIDA EN RB1 SE APAGA LA ALARMA
    GOTO SEGUIR_NO_EDITAR
    
    BTFSC  PORTB_ACTUAL,1
    GOTO    SEGUIR_NO_EDITAR
    
    BCF	FLAGS,6
    BCF	PORTE,1
    MOVLW   VALOR_CONTADOR_ALARMA
    MOVWF   CONTADOR_ALARMA
    MOVLW   VALOR_SEGUNDOS_ALARMA
    MOVWF   SEGUNDOS_ALARMA
    
    SEGUIR_NO_EDITAR:
     MOVF    SEGUNDOS_TEMPORIZADOR,W
    MOVWF   DISPLAY_1
    MOVF    MINUTOS_TEMPORIZADOR,W
    MOVWF   DISPLAY_2
     CALL DISPLAY_NO_EDITAR
    GOTO LOOP
    
    SI_EDITAR_TEMPORIZADOR:
    CALL EDITAR_TEMPORIZADOR
     MOVF    SEGUNDOS_TEMPORIZADOR_EDITAR,W
    MOVWF   DISPLAY_1
    MOVF    MINUTOS_TEMPORIZADOR_EDITAR,W
    MOVWF   DISPLAY_2
    CALL DISPLAY_EDITAR
    
    GOTO LOOP
    

;------------------- SUBRUTINAS DE VISTA ------------------------------------------------
    DISPLAY_NO_EDITAR:
    BTFSC   CONTROL_DISPLAY,1
    GOTO DISP_1_2
    BTFSC   CONTROL_DISPLAY,2
    GOTO DISP_2_1
    BTFSC   CONTROL_DISPLAY,3
    GOTO DISP_2_2
    ;SI NINGUNO DE LOS ANTERIORES SE DIO ES QUE SE TENIA QUE ENTRAR AQUI
    MOVF    DISPLAY_1,W
    ANDLW   B'00001111' ;SE CONSERVA EL NIBBLE MENOS SIGNFICATIVO
    CALL TABLA
    GOTO MOSTRAR_VALOR
    
    DISP_1_2
    SWAPF   DISPLAY_1,W 
    ANDLW B'00001111'; SE CONSERVA EL VALOR DEL BIT MENOS SIGNIFICATIVO
    CALL TABLA
    GOTO MOSTRAR_VALOR
    
    DISP_2_1
    MOVF DISPLAY_2,W
    ANDLW   B'00001111'
    CALL TABLA
    GOTO MOSTRAR_VALOR
    
   DISP_2_2
   SWAPF   DISPLAY_2,W
   ANDLW   B'00001111'
   CALL TABLA
   
    MOSTRAR_VALOR
    CLRF    PORTD
    MOVWF   PORTC
    MOVF    CONTROL_DISPLAY,W
    MOVWF   PORTD
    RETURN
    
DISPLAY_EDITAR:
    MOVLW .0 ;W SOLO SE SOBREESCRIBE SI EL DISPLAY TIENE QUE ESTAR ENCENDIDO
    BTFSC   CONTROL_DISPLAY,1
    GOTO DISPLAY_1_2_EDITAR
    BTFSC   CONTROL_DISPLAY,2
    GOTO DISPLAY_2_1_EDITAR
    BTFSC   CONTROL_DISPLAY,3
    GOTO DISPLAY_2_2_EDITAR
    
    ;SI SE LLEGA AQUI ES QUE ESTE ERA EL QUE SE TENIA QUE ENCENDER
    BTFSS   FLAGS,4
    GOTO    DISPLAY_1_1_EDITAR_NORMAL
    
    
    BTFSS   FLAGS,2
    GOTO MOSTRAR_DISPLAY_EDITAR
    DISPLAY_1_1_EDITAR_NORMAL
    MOVF    DISPLAY_1,W
    ANDLW   B'00001111' ;SE CONSERVA EL NIBBLE MENOS SIGNFICATIVO
    CALL TABLA
    GOTO MOSTRAR_DISPLAY_EDITAR
    
    DISPLAY_1_2_EDITAR
     BTFSS   FLAGS,4
    GOTO    DISPLAY_1_2_EDITAR_NORMAL
    
    BTFSS   FLAGS,2
    GOTO MOSTRAR_DISPLAY_EDITAR
    DISPLAY_1_2_EDITAR_NORMAL
    SWAPF   DISPLAY_1,W 
    ANDLW B'00001111'; SE CONSERVA EL VALOR DEL BIT MENOS SIGNIFICATIVO
    CALL TABLA
    GOTO MOSTRAR_DISPLAY_EDITAR
    
    DISPLAY_2_1_EDITAR
     BTFSS   FLAGS,4
    GOTO    DISPLAY_2_1_EDITAR_NORMAL
    
    BTFSC   FLAGS,2
    GOTO MOSTRAR_DISPLAY_EDITAR ;SE APAGAN TODOS LOS BITS DEL PUERTO C
    DISPLAY_2_1_EDITAR_NORMAL
    MOVF DISPLAY_2,W
    ANDLW   B'00001111'
    CALL TABLA
    GOTO MOSTRAR_DISPLAY_EDITAR
    
    DISPLAY_2_2_EDITAR
    BTFSS   FLAGS,4
    GOTO    DISPLAY_2_2_EDITAR_NORMAL
    
    BTFSC   FLAGS,2
    GOTO MOSTRAR_DISPLAY_EDITAR ;SE APAGAN TODOS LOS BITS DEL PUERTO C
    DISPLAY_2_2_EDITAR_NORMAL
    SWAPF   DISPLAY_2,W
    ANDLW   B'00001111'
    CALL TABLA
    
    MOSTRAR_DISPLAY_EDITAR
    CLRF    PORTD
    MOVWF   PORTC
    MOVF    CONTROL_DISPLAY,W
    MOVWF   PORTD
    RETURN
;--------------------- SUBRUTINAS PRA EL CONTROL DE LOS DATOS ------------------------------------------------------------
CORRECCION_BCD_AUMENTO:
    ;PARA ESTA RUTA SE USA EL PASO POR REFERENCIA DE PARAMETRO, USANDO EL DIRECCIONAMIENTO INDIRECTO
    BCF	FLAGS_2,1 ;PARA QUE NO SE TENGA QUE LIMPIAR DE FORMA EXTERNA
    MOVF    INDF,W ;VALOR AL QUE APUNTA EL SFR SE MUEVE W 
    ANDLW   B'00001111' ;SE QUEDA CON EL DIGITO MENOR
    SUBLW   .10	;SI ES 10 ES QUE SE DEBE DE CORREGIR
    BTFSS   STATUS,2
    RETURN
    
    BSF	FLAGS_2,1 ;SE INDICA QUE SE TUVO QUE REALIZAR LA CORRECCION
    MOVLW   .6 
    ADDWF   INDF,F ;SE LE SUMA 6 PARA CORREGIR
    RETURN ;SE REGRESA

CORRECCION_BCD_DISMINUCION:
    ;PARA ESTA RUTA SE USA EL PASO POR REFERENCIA DE PARAMETRO, USANDO EL DIRECCIONAMIENTO INDIRECTO
    BCF	FLAGS_2,0 ;PARA QUE NO SE TENGA QUE LIMPIAR DE FORMA EXTERNA
    MOVF    INDF,W ;VALOR AL QUE APUNTA EL SFR SE MUEVE W 
    ANDLW   B'00001111' ;SE QUEDA CON EL DIGITO MENOR
    SUBLW   .15	;SI ES 15 ES QUE SE DEBE DE CORREGIR
    BTFSS   STATUS,2
    RETURN
    
    BSF	FLAGS_2,0 ;SE INDICA QUE SE TUVO QUE REALIZAR LA CORRECCION
    MOVLW   .6 
    SUBWF   INDF,F ;SE LE RESTA 6 PARA CORREGIR
    RETURN ;SE REGRESA
    
VALOR_TEMPORIZADOR: ;EN ESTA SUBRUTINA UNICAMENTE SE CORRIGEN LOS VALORES BCD
    ;EL VALOR DE LA VARIABLE SEGUNDOS SE DISMINUYE EN LA INTERRUPCION DEL TMR2 AQUI SE CORRIGE EL BCD
    MOVLW   SEGUNDOS_TEMPORIZADOR
    MOVWF   FSR ;SE PASA LA DIRECCION DE ESTA VARIABLE A SFR PARA EL PASO POR REFERENCIA
    CALL    CORRECCION_BCD_DISMINUCION ;SE CORRIGE EL BCD DE SEGUNDOS
   
    MOVLW MINUTOS_TEMPORIZADOR
    MOVWF   FSR ;PASO POR REFERENCIA
    CALL    CORRECCION_BCD_DISMINUCION ;SE CORRIGE EL BCD
    RETURN
    
    
; SUBRUTINA ENCARGADA DE REALIZAR EL CONTROL DE LA HORA Y LA FECHA DEL RELOJ
VALOR_FECHA:
    BCF	FLAGS,0 ;SE APAGA LA BANDERA DE CAMBIO DE FECHA
    INCF    DIA,F ;SE AUMENTA EL VALOR
   
    MOVLW   DIA
    MOVWF   FSR ;PASO POR REFERENCIA
    CALL    CORRECCION_BCD_AUMENTO ;SE CORRIGE LA CODIFICACION BCD
    ;AQUI SE VERIFICA SI EL NUEVO VALOR PARA EL DIA ES VALIDO O SI YA SE ENCUENTRA EN EL MES SIGUIENTE
    CALL TABLA_MESES
    SUBWF   DIA,W
    BTFSS   STATUS,2 ;SI LO ANTERIOR DA 0 SE SALTA LA SIGUENTE INSTRUCCION
    RETURN
    
    ;SI  SE SALTA AQUI SIGUE
    MOVLW   .1
    MOVWF   DIA
    INCF    MES_BIN,F
    INCF    MES_BCD,F
    ;SE CORRIGE EL BCD SI ES NECESARIO
    MOVLW   MES_BCD
    MOVWF   FSR ;PASO POR REFERENCIA
    CALL    CORRECCION_BCD_AUMENTO ;SE CORRIGE
    
    ;SE VERIFICA QUE NO SE PASE DE 12 MESES
    MOVLW   .13
    SUBWF   MES_BIN,W
    BTFSS   STATUS,2 ;SI DA 0 SE SALTA LA SIGUEINTE INSTRUCCION 
    RETURN
    
    MOVLW .1
    MOVWF   MES_BIN
    MOVWF   MES_BCD
    RETURN ;FIN DEL VALOR FECHA

    
VALOR_RELOJ:
    INCF    MINUTOS,F
    CLRF    SEGUNDOS
    
    MOVLW   MINUTOS
    MOVWF   FSR ;PASO POR REFERENCIA DE LA VARIABLE
    CALL    CORRECCION_BCD_AUMENTO ;SE CORRIGE
    ;SE VERIFICA QUE LLEGUE A 60 
    MOVLW B'01100000' ;60 EN BCD
    SUBWF  MINUTOS,W
    BTFSS   STATUS,2 
    RETURN ;SI NO ES 60 NO ES NECESARIO SUMAR 1 A LAS HORAS
    ; SI SI LLEGO A 60 SE INCREMENTAN LAS HORAS 
    CLRF    MINUTOS
    INCF    HORAS, F
    ;SE VERIFICA QUE NO SE LLEGE A 24
    MOVLW B'00100100' ;24 EN BCD
    SUBWF   HORAS,W
    BTFSS  STATUS, 2 ;SI LLEGA A ESTO SE LIMPIA EL VALOR DE HORAS Y TERMINA LA RUTINA
    GOTO CORRECCION_HORA
    
    CLRF    HORAS
    BSF	FLAGS,0 ;ESTE BIT INDICA QUE HAY QUE INCREMENTAR LA FECHA
    RETURN ;SI ES 24 SE SABE QUE NO ES NECESARIO CORREGIR LA CODIFICACION BCD
    
    CORRECCION_HORA
    MOVLW  HORAS
    MOVWF   FSR ;PASO POR REFERNCIA
    CALL    CORRECCION_BCD_AUMENTO ;SE CORRIGE EL BCD
    RETURN ;FIN DE LA CORRECCION

EDITAR_HORA:
    ;CUANDO SE ENTRA EN ESTE MODO RB2 REALIZA LA FUNCION DE SELECTOR DE EDICION DE HORA O MINUTO
    ;RB5 DETERMINA CUANDO SE SALE DE ESTE MODO, 
    BTFSC   FLAGS,3
    GOTO SI_YA_SE_ENTRO_ANTES
    ;-----------------SE ENTRA A ESTA PARTE SOLO CUANDO ES LA PRIMERA VEZ --------------------------------------------
    BSF	FLAGS,3 ;PARA QUE NO SE VUELVA A ENTRAR AQUI HASTA QUE SE SALGA DEL MODO EDICION
    MOVF    HORAS,W
    MOVWF   HORA_EDITAR
    
    BSF	T1CON,0 ;SE ACTIVA EL TIMER 1
    
    MOVF    MINUTOS,W
    MOVWF   MINUTO_EDITAR ;SE GUARDAN LOS VALORES EN EL INSTANTE QUE SE ENTRA AL MODO DE EDICION
   
    MOVF    PORTB_ACTUAL,W ; SE ACTUALIZAN ESTOS REGISTROS YA QUE SI NO SE HACE, AL SER LA CONDICION
    MOVWF   PORTB_ANTERIOR ;DE ENTRADA IGUAL QUE LA DE SALIDA, SOLO SE VA A EJECUTAR 1 VEZ
    CALL DELAY_US ;SE ESPERA UN MOMENTO
    ;CALL DELAY_US ;SE ESPERA UN MOMENTO
    MOVF    PORTB,W
    MOVWF   PORTB_ACTUAL
    ;-----------------------------------------------------------------------------------------------------------------------------------
    SI_YA_SE_ENTRO_ANTES
    ;-------------------- SE EVITA EL PARPADEO DE LOS LEDS DEL CENTRO ----------------------------------------
    BSF	PORTE,2
    ;-------------------------------------------------------------------------------------------------------------------------------
    ;SE HACE EL CAMBIO ENTRE 0 O 1 PARA EL VALOR A EDITAR (SE VERIFICA QUE HAYA OCURRIDO FLANCO DE SUBIDA)
    BTFSC PORTB_ANTERIOR,2 ;RB2 
    GOTO    CAMBIAR
    
    BTFSS   PORTB_ACTUAL,2 
    GOTO CAMBIAR
    ;---------SE HACE EL CAMBIO DEL VALOR A EDITAR -----------------
    BTFSS   FLAGS,2 ;SE VE SI ESTA EN 1
    GOTO CAMBIAR_0
    
    BCF	FLAGS,2 ;SE  LIMPIA SI ESTA EN 1
    GOTO    CAMBIAR
    CAMBIAR_0  ;SI NO ESTA EN 1 ESTA EN CERO ENTONCES 
    BSF	FLAGS,2 
    ;-------------------------------------------------------------------------------------------
    CAMBIAR
    BTFSC   FLAGS,2 ; SE VE SI LA EDICION ESTA EN HORAS(1) O MINUTOS(0)
    GOTO EDIT_HOUR
    
    
    EDIT_MINUTE
    ;---------SE VERIFICA SI SE DEBE DECREMENTAR--------------------------------------
    DECREMENTAR_MINUTE
    BTFSC   PORTB_ANTERIOR,0
    GOTO    INCREMENTAR_MINUTE
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_MINUTE
    ;--------SI HUBO UN FLANCO DE SUBIDA SE EN RB0 SE EJECUTA ESTO  DECREMENTAR ---------------
    DECF    MINUTO_EDITAR,F
    ;--- CORRECCION DEL BCD ----------
    ;PRIMERO SE VERIFICA SI SE PASO DE 00 A 59
    MOVLW .255 ;ESTE VALOR HABRIA SI SE LE RESTA 1 A 0X00H 
    SUBWF   MINUTO_EDITAR,W
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_MINUTO_EDITAR
    
    MOVLW   B'01011001' ;59 EN BCD
    MOVWF   MINUTO_EDITAR
    
    CORREGIR_BCD_MINUTO_EDITAR
    MOVLW   B'00001111' 
    ANDWF   MINUTO_EDITAR,W;SE CONSERVA EL PRIMER DIGITO
    SUBLW   .15 ;ESTE VALOR DARIA SI PASA DE 0 A 9 
    BTFSS   STATUS,2 ;SI DA 1 SE SALTA LA INSTRUCCION (SE CORRIGE EL BCD)
    GOTO INCREMENTAR_MINUTE
    
    MOVLW   .6 
    SUBWF   MINUTO_EDITAR,F ;SE COLOCA  EN 9 COMO DEBERIA DE SER
    
    INCREMENTAR_MINUTE
    ;---------------------- SE VERIFICA SI SE DEBE INCREMENTAR -------------------------
    BTFSC   PORTB_ANTERIOR,1  ;SI EL ANTERIOR ERA 0 SE VERIFICA QUE EL SIGUIENTE SEA 1
    GOTO    SALIR
    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR
    
    ;----------- SI SE LLEGA HASTA AQUI ES QUE SE DEBIA INCREMENTAR ----------------------
    INCF    MINUTO_EDITAR,F
    
    ;----------------CORRECION DE BCD CUANDO SE PASA DE X9 A (X+1)0
    MOVLW   B'00001111' ;SE CONSERVA EL PRIMER DIGITO
    ANDWF   MINUTO_EDITAR,W
    SUBLW .10 ;SI DA 0 SE DEBE SUMAR LAS DECENAS
    BTFSS   STATUS,2 ;SI DA 0 SE SALTA ESTA INSTRUCCION
    GOTO VERIFICAR_OVERFLOW_MINUTO_EDITAR
    
    MOVLW   .6
    ADDWF   MINUTO_EDITAR,F ;SE ARREGLA EL BCD
    ;-----------------VERIFICAR SI SE PASA DE 59 A 00
    VERIFICAR_OVERFLOW_MINUTO_EDITAR
    MOVLW B'01100000' ;60 EN BCD
    SUBWF   MINUTO_EDITAR,W
    BTFSC   STATUS,2 ;SI DA 0 SE REINICIA
    CLRF    MINUTO_EDITAR
    
    GOTO SALIR 
    
    EDIT_HOUR
    ;SI HUBO UN FLANCO DE SUBIDA EN RB0 SE DECREMENTA EN 1 LAS HORAS
    ;------SE VERIFICA SI SE DEBE DECREMENTAR------------------
    BTFSC   PORTB_ANTERIOR,0 ;SE MIRA SI HUBO UN CAMBIO DE 0 A 1 (FLANCO DE SUBIDA
    GOTO    INCREMENTAR_HOUR
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_HOUR
    ;--------SI HUBO UN FLANCO DE SUBIDA SE EN RB0 SE EJECUTA ESTO ---------------
    DECF    HORA_EDITAR,F
    ;--- CORRECCION DEL BCD ----------
    ;PRIMERO SE VERIFICA SI SE PASO DE 00 A 24
    MOVLW .255 ;ESTE VALOR HABRIA SI SE LE RESTA 1 A 0X00H 
    SUBWF   HORA_EDITAR,W
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_HORA_EDITAR
    
    MOVLW   B'00100011' ;23 EN BCD
    MOVWF   HORA_EDITAR
    
    CORREGIR_BCD_HORA_EDITAR
    MOVLW   B'00001111' 
    ANDWF   HORA_EDITAR,W;SE CONSERVA EL PRIMER DIGITO
    SUBLW   .15 ;ESTE VALOR DARIA SI PASA DE 0 A 9 
    BTFSS   STATUS,2 ;SI DA 1 SE SALTA LA INSTRUCCION
    GOTO INCREMENTAR_HOUR
    
    MOVLW   .6 
    SUBWF   HORA_EDITAR,F ;SE COLOCA  EN 9 COMO DEBERIA DE SER
    
    INCREMENTAR_HOUR
    ;---------------SE VERIFICA SI SE DEBE INCREMENTAR-----------------------------------------------
    BTFSC   PORTB_ANTERIOR,1
    GOTO    SALIR
    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR
    
    ;----------- SI SE LLEGA HASTA AQUI ES QUE SE DEBIA INCREMENTAR ----------------------
    INCF    HORA_EDITAR,F
    
    ;----------------CORRECION DE BCD CUANDO SE PASA DE X9 A (X+1)0
    MOVLW   B'00001111' ;SE CONSERVA EL PRIMER DIGITO
    ANDWF   HORA_EDITAR,W
    SUBLW .10 ;SI DA 0 SE DEBE SUMAR LAS DECENAS
    BTFSS   STATUS,2 ;SI DA 0 SE SALTA ESTA INSTRUCCION
    GOTO VERIFICAR_OVERFLOW_HORA_EDITAR
    
    MOVLW   .6
    ADDWF  HORA_EDITAR,F ;SE ARREGLA EL BCD
    ;-----------------VERIFICAR SI SE PASA DE 23 A 00
    VERIFICAR_OVERFLOW_HORA_EDITAR
    MOVLW B'00100100' ;24 EN BCD
    SUBWF   HORA_EDITAR,W
    BTFSC   STATUS,2 ;SI DA 0 SE REINICIA
    CLRF    HORA_EDITAR
    ;------- LUEGO DE TODO SE LLEGA A ESTE PUNTO --------------------------------------------------
    SALIR 
    ;------- SE VERIFICA QUE SI SE HA SALIDO SOLO PULSANDO EL BOTON (SALIDA SIN GUARDAR DATOS)--------
    BTFSC   PORTB_ANTERIOR,5
    RETURN
    
    BTFSS   PORTB_ACTUAL,5
    RETURN
    
  
    ;-------------- SI HUBO FLANCO DE SUBIDA SE LLEGA A ESTE PUNTO -----------------------------------
    BCF FLAGS,3 ;SE DESHABILITA EL BIT DE VERIFICACION DE PRIMERA VEZ ENTRANDO A EDITAR HORA
    BCF FLAGS,2 ;SE COLOCA EN EDITAR MINUTO/DIA 
    BCF FLAGS,1 ;SE DESHABILITA EL MODO DE EDICION
    
   MOVLW   VALOR_TMR1H
   MOVWF   TMR1H
   MOVLW   VALOR_TMR1L
    MOVWF   TMR1L
    
    MOVLW   VALOR_TMR1
    MOVWF   CONTADOR_TMR1
    BCF	T1CON,0 ;SE APAGA EL TMR1
    
    RETURN 
    
EDITAR_FECHA:
        ;CUANDO SE ENTRA EN ESTE MODO RB2 REALIZA LA FUNCION DE SELECTOR DE EDICION DE HORA O MINUTO
    ;RB5 DETERMINA CUANDO SE SALE DE ESTE MODO, 
    BTFSC   FLAGS,3
    GOTO SI_YA_SE_ENTRO_ANTES_FECHA
    ;-----------------SE ENTRA A ESTA PARTE SOLO CUANDO ES LA PRIMERA VEZ --------------------------------------------
    BSF	FLAGS,3 ;PARA QUE NO SE VUELVA A ENTRAR AQUI HASTA QUE SE SALGA DEL MODO EDICION
    MOVF    DIA,W
    MOVWF   DIA_EDITAR
    
    BSF	T1CON,0 ;SE ACTIVA EL TIMER 1
    
    MOVF    MES_BCD,W
    MOVWF   MES_BCD_EDITAR ;SE GUARDAN LOS VALORES EN EL INSTANTE QUE SE ENTRA AL MODO DE EDICION
   
    MOVF    MES_BIN,W
    MOVWF   MES_BIN_EDITAR
    MOVF    PORTB_ACTUAL,W ; SE ACTUALIZAN ESTOS REGISTROS YA QUE SI NO SE HACE, AL SER LA CONDICION
    MOVWF   PORTB_ANTERIOR ;DE ENTRADA IGUAL QUE LA DE SALIDA, SOLO SE VA A EJECUTAR 1 VEZ
    CALL DELAY_US ;SE ESPERA UN MOMENTO
    ;CALL DELAY_US ;SE ESPERA UN MOMENTO
    MOVF    PORTB,W
    MOVWF   PORTB_ACTUAL
    ;
    
    ;SE VERIFICA SI ES EL MINUTO O LA HORA QUE SE VA A EDITAR
    ;-----------------------------------------------------------------------------------------------------------------------------------
    SI_YA_SE_ENTRO_ANTES_FECHA
    ;-------------------- SE EVITA EL PARPADEO DE LOS LEDS DEL CENTRO ----------------------------------------
    BSF	PORTE,2
    ;-------------------------------------------------------------------------------------------------------------------------------
    ;SE HACE EL CAMBIO ENTRE 0 O 1 PARA EL VALOR A EDITAR (SE VERIFICA QUE HAYA OCURRIDO FLANCO DE SUBIDA)
    BTFSC PORTB_ANTERIOR,2 ;RB2 
    GOTO    CAMBIAR_FECHA
    
    BTFSS   PORTB_ACTUAL,2 
    GOTO CAMBIAR_FECHA
    ;---------SE HACE EL CAMBIO DEL VALOR A EDITAR -----------------
    BTFSS   FLAGS,2 ;SE VE SI ESTA EN 1
    GOTO CAMBIAR_1
    
    BCF	FLAGS,2 ;SE  LIMPIA SI ESTA EN 1
    GOTO    CAMBIAR_FECHA
    CAMBIAR_1  ;SI NO ESTA EN 1 ESTA EN CERO ENTONCES 
    BSF	FLAGS,2 
    ;-------------------------------------------------------------------------------------------
    CAMBIAR_FECHA
    CALL TABLA_MESES ;SE GUARDA PARA SABER EL NUMERO MAXIMO DE DIAS EN EL MES SELECCIONADO
    MOVWF DIA_MAX ;SE GUARDA EN DIA MAX
    
    ;--------SE VERIFICA QUE EDITAR DIA NO SEA MAYOR QUE EL DIA ESTABLECIDO PARA EL MES-------------
     MOVF	DIA_MAX,W
    SUBWF DIA_EDITAR,W
    BTFSS   STATUS, C ; SI C=1  QUIERE DECIR QUE DIA_MAX<= DIA_EDITAR Y ENTONCES SE CORRIGE
    GOTO    DECISION_FECHA
    
    DECF    DIA_MAX,W
    MOVWF   DIA_EDITAR
    
    DECISION_FECHA
    BTFSS   FLAGS,2 ;SE MIRA SI LO QUE SE DESEA EDITAR ES EL DIA O EL MES
    GOTO EDIT_MONTH
    GOTO EDIT_DAY
    
    EDIT_DAY
    ;----------------------------------- DISMINUIR --------------------------------------------------------------
    DECREMENTAR_DAY
   ; --------------------------------SE COMPRUEBA QUE HAYA UN FLANCO DE SUBIDA EN RB0 ----------------------
    BTFSC   PORTB_ANTERIOR,0
    GOTO    INCREMENTAR_DAY
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_DAY
    ;--------SI HUBO UN FLANCO DE SUBIDA SE EN RB0 SE EJECUTA ESTO  DECREMENTAR ---------------
    DECF    DIA_EDITAR,F
    ;--- CORRECCION DEL BCD -----------------
    ;PRIMERO SE VERIFICA SI SE PASO DE 00 A DIA_MAX
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_DIA_EDITAR
    
   DECF	DIA_MAX,W
    MOVWF   DIA_EDITAR
    ;--------------------------------------------------
    
    CORREGIR_BCD_DIA_EDITAR
    MOVLW   B'00001111' 
    ANDWF   DIA_EDITAR,W;SE CONSERVA EL PRIMER DIGITO
    SUBLW   .15 ;ESTE VALOR DARIA SI PASA DE 0 A 9 
    BTFSS   STATUS,2 ;SI DA 1 SE SALTA LA INSTRUCCION (SE CORRIGE EL BCD)
    GOTO INCREMENTAR_DAY
    
    MOVLW   .6 
    SUBWF   DIA_EDITAR,F ;SE COLOCA  EN 9 COMO DEBERIA DE SER
    ;------------------------------------------------------------------------------------------------------------------------------
    INCREMENTAR_DAY
    ;---------------------- SE VERIFICA SI SE DEBE INCREMENTAR(FLANCO DE SUBIDA EN RB1) -------------------------
    BTFSC   PORTB_ANTERIOR,1  ;SI EL ANTERIOR ERA 0 SE VERIFICA QUE EL SIGUIENTE SEA 1
    GOTO    SALIR_FECHA
    ;SI LA CONDICION 0 ANTES Y 1 DESPUES NO SE DA ENTONCES NO SE EJECUTA LA SECCION QUE LE SIGUE    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR_FECHA
    ;------------------------------ SI SE VERIFICA QUE SE DEBE INCREMENTAR SE EJECUTA ESTO------------------
    INCF    DIA_EDITAR,F 
    
   ;SE CORRIGE EL BCD
    MOVF    DIA_EDITAR,W
    ANDLW   B'00001111' ;CONSERVAR EL PRIMER DIGITO
    SUBLW   .10 
    BTFSS   STATUS,2 ; SE SE LLEGA  A 10 SE CORRIGE
    GOTO    OVERFLOW_DIA
    
    MOVLW   .6
    ADDWF   DIA_EDITAR,F 
    
    OVERFLOW_DIA
    ;SE VERIFICA QUE NO SE HAYA PASADO DEL DIA MAXIMO PERMITIDO
    MOVF    DIA_MAX,W
    SUBWF   DIA_EDITAR,W
    BTFSS STATUS,2 ; Z=1 SI DIA_MAX = DIA_EDITAR    SE LIMPIAN LOS DIAS
    GOTO SALIR_FECHA ;SE SALE DE LA RUTINA
    
    MOVLW .1
    MOVWF   DIA_EDITAR
    GOTO    SALIR_FECHA
    
    EDIT_MONTH
   ; --------------------------------SE COMPRUEBA QUE HAYA UN FLANCO DE SUBIDA EN RB0 ----------------------
    BTFSC   PORTB_ANTERIOR,0
    GOTO    INCREMENTAR_MONTH
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_MONTH
    ;------------------------------------------------------------------------------------------------------------------------------------
    ;--------------------------------- SE DECREMENTA SI SE DETECTA UN FLANCO DE SUBIDA------------------------
    DECF    MES_BCD_EDITAR,F
    DECF    MES_BIN_EDITAR,F
    
    
    ;PRIMERO SE VERIFICA SI SE PASO DE 01 A 12
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_MES_EDITAR
    
    MOVLW   .12
    MOVWF   MES_BIN_EDITAR
    MOVLW   B'00010010' ; 12 EN BCD
    MOVWF   MES_BCD_EDITAR
    
    CORREGIR_BCD_MES_EDITAR
    ;--------- SE CORRIGE EL BCD -------------------------------------------------------------
    MOVLW B'00001111'
    ANDWF  MES_BCD_EDITAR,W
    SUBLW   .15
    BTFSS   STATUS,2
    GOTO    INCREMENTAR_MONTH
    
    MOVLW   .6
    SUBWF   MES_BCD_EDITAR
    ;-------------------------------------------------------------------------------------------------
    INCREMENTAR_MONTH
      ;---------------------- SE VERIFICA SI SE DEBE INCREMENTAR(FLANCO DE SUBIDA EN RB1) -------------------------
    BTFSC   PORTB_ANTERIOR,1  ;SI EL ANTERIOR ERA 0 SE VERIFICA QUE EL SIGUIENTE SEA 1
    GOTO    SALIR_FECHA
    ;SI LA CONDICION 0 ANTES Y 1 DESPUES NO SE DA ENTONCES NO SE EJECUTA LA SECCION QUE LE SIGUE    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR_FECHA
    ;------------------------------ SI SE VERIFICA QUE SE DEBE INCREMENTAR SE EJECUTA ESTO------------------
    INCF    MES_BCD_EDITAR,F
    INCF    MES_BIN_EDITAR,F
    
    ;-------- SE VERIFICA EL PASO DE 12 A 00 ---------------------------
    MOVLW   .13
    SUBWF   MES_BIN_EDITAR,W
    BTFSS   STATUS,2
    GOTO    CORREGIR_BCD_MES_INCREMENTAR
    
    MOVLW .1
    MOVWF MES_BIN_EDITAR
    MOVWF MES_BCD_EDITAR
    
    CORREGIR_BCD_MES_INCREMENTAR
    MOVF    MES_BCD_EDITAR,W
    ANDLW B'00001111' 
    SUBLW   .10
    BTFSS   STATUS,2
    GOTO SALIR_FECHA
    
    MOVLW   .6
    ADDWF   MES_BCD_EDITAR,F
    ;------- LUEGO DE TODO SE LLEGA A ESTE PUNTO --------------------------------------------------
    SALIR_FECHA 
    ;------- SE VERIFICA QUE SI SE HA SALIDO SOLO PULSANDO EL BOTON (SALIDA SIN GUARDAR DATOS)--------
    BTFSC   PORTB_ANTERIOR,5
    RETURN
   ; SI NO HAY FLANCO DE SUBIDA SOLO SE SALE DE LA RUTINA PERO LA CONDICION DE EDICION SIEMPRE
   ;ESTA ACTIVA
    BTFSS   PORTB_ACTUAL,5
    RETURN
   
    ;-------------- SI HUBO FLANCO DE SUBIDA SE LLEGA A ESTE PUNTO -----------------------------------
    BCF FLAGS,3 ;SE DESHABILITA EL BIT DE VERIFICACION DE PRIMERA VEZ ENTRANDO A EDITAR HORA
    BCF FLAGS,2 ;SE COLOCA EN EDITAR MINUTO/DIA 
    BCF FLAGS,1 ;SE DESHABILITA EL MODO DE EDICION
    
   MOVLW   VALOR_TMR1H
   MOVWF   TMR1H
   MOVLW   VALOR_TMR1L
    MOVWF   TMR1L
    
    MOVLW   VALOR_TMR1
    MOVWF   CONTADOR_TMR1
    BCF	T1CON,0 ;SE APAGA EL TMR1
    
    RETURN
    
   EDITAR_TEMPORIZADOR:
    ;CUANDO SE ENTRA EN ESTE MODO RB2 REALIZA LA FUNCION DE SELECTOR DE EDICION DE HORA O MINUTO
    ;RB5 DETERMINA CUANDO SE SALE DE ESTE MODO, 
    BTFSC   FLAGS,3
    GOTO SI_YA_SE_ENTRO_ANTES_TEMPORIZADOR
    ;-----------------SE ENTRA A ESTA PARTE SOLO CUANDO ES LA PRIMERA VEZ --------------------------------------------
    BSF	FLAGS,3 ;PARA QUE NO SE VUELVA A ENTRAR AQUI HASTA QUE SE SALGA DEL MODO EDICION
     MOVF    SEGUNDOS_TEMPORIZADOR,W
    SUBLW   .0 ;SI ES CERO LO ANTERIOR Z=1 
    BTFSS   STATUS,2 
    GOTO    IF_NOT_CERO
    ;ya que el tiempo MINIMO PARA CONFIGURAR ES 1 SEGUNDO SE TIENE QUE VERIFICAR QUE EL TEMPORIZADOR
    ;NUNCA SE COLOQUE EN 00 MINUTOS Y 00 SEGUNDOS AL EDITAR,
    MOVF    MINUTOS_TEMPORIZADOR,W
    SUBLW   .0 ;UNICAMENTE SI W ES SERO Z=1
    BTFSS   STATUS,2
    GOTO IF_NOT_CERO
    
    ;SI SI ESTA EN 0 MINUTOS SE CONTINUA AQUI
    MOVF    SEGUNDOS_TEMPORIZADOR,W
    SUBLW .0 ;SOLO SI W ES CERO Z=1
    BTFSS   STATUS,2
    GOTO IF_NOT_CERO
    
    MOVLW   .1
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    GOTO CONTINUE
    IF_NOT_CERO
    MOVF    SEGUNDOS_TEMPORIZADOR,W
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    CONTINUE
    BSF	T1CON,0 ;SE ACTIVA EL TIMER 1
    
    MOVF    MINUTOS_TEMPORIZADOR,W
    MOVWF   MINUTOS_TEMPORIZADOR_EDITAR ;SE GUARDAN LOS VALORES EN EL INSTANTE QUE SE ENTRA AL MODO DE EDICION
   
    MOVF    PORTB_ACTUAL,W ; SE ACTUALIZAN ESTOS REGISTROS YA QUE SI NO SE HACE, AL SER LA CONDICION
    MOVWF   PORTB_ANTERIOR ;DE ENTRADA IGUAL QUE LA DE SALIDA, SOLO SE VA A EJECUTAR 1 VEZ
    CALL DELAY_US ;SE ESPERA UN MOMENTO
    ;CALL DELAY_US ;SE ESPERA UN MOMENTO
    MOVF    PORTB,W
    MOVWF   PORTB_ACTUAL
    ;-----------------------------------------------------------------------------------------------------------------------------------
    SI_YA_SE_ENTRO_ANTES_TEMPORIZADOR
    ;-------------------- SE EVITA EL PARPADEO DE LOS LEDS DEL CENTRO ----------------------------------------
    BSF	PORTE,2
    ;-------------------------------------------------------------------------------------------------------------------------------
    ;SE HACE EL CAMBIO ENTRE 0 O 1 PARA EL VALOR A EDITAR (SE VERIFICA QUE HAYA OCURRIDO FLANCO DE SUBIDA)
    BTFSC PORTB_ANTERIOR,2 ;RB2 
    GOTO    CAMBIAR_TEMPORIZADOR
    
    BTFSS   PORTB_ACTUAL,2 
    GOTO CAMBIAR_TEMPORIZADOR
    ;---------SE HACE EL CAMBIO DEL VALOR A EDITAR -----------------
    BTFSS   FLAGS,2 ;SE VE SI ESTA EN 1
    GOTO CAMBIAR_0_TEMPORIZADOR
    
    BCF	FLAGS,2 ;SE  LIMPIA SI ESTA EN 1
    GOTO    CAMBIAR_TEMPORIZADOR
    CAMBIAR_0_TEMPORIZADOR  ;SI NO ESTA EN 1 ESTA EN CERO ENTONCES 
    BSF	FLAGS,2 
    ;-------------------------------------------------------------------------------------------
    CAMBIAR_TEMPORIZADOR
    BTFSC   FLAGS,2 ; SE VE SI LA EDICION ESTA EN HORAS(1) O MINUTOS(0)
    GOTO EDIT_MINUTE_TEMPORIZADOR
    
    
    EDIT_SECOND
    ;---------SE VERIFICA SI SE DEBE DECREMENTAR--------------------------------------
    DECREMENTAR_SECOND
    BTFSC   PORTB_ANTERIOR,0
    GOTO    INCREMENTAR_SECOND
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_SECOND
    ;--------SI HUBO UN FLANCO DE SUBIDA SE EN RB0 SE EJECUTA ESTO  DECREMENTAR ---------------
    DECF    SEGUNDOS_TEMPORIZADOR_EDITAR,F
    
    ;SE VERIFICA SI MINUTOS_TEMPORIZADOR_EDITAR ES DIFERENCE DE CERO
    MOVF    MINUTOS_TEMPORIZADOR_EDITAR,W
    SUBLW   .0 ;SOLO SI ES 0 Z=1 
    BTFSS   STATUS,2 
    GOTO CORREGIR_BCD_TEMP_SEG_DEC
    ;-------- SI MINUTOS ES CERO SE EJECUTA ESTO ----------------- 
    MOVF    SEGUNDOS_TEMPORIZADOR_EDITAR,W
    SUBLW   .0 ;SI ESTO DA CERO
    BTFSS   STATUS,2
    GOTO    CORREGIR_BCD_TEMP_SEG_DEC
    
    MOVLW   B'01011001' ;59 EN BCD
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    
    CORREGIR_BCD_TEMP_SEG_DEC
    ;--- CORRECCION DEL BCD ----------
    ;PRIMERO SE VERIFICA SI SE PASO DE 00 A 59
    MOVLW .255 ;ESTE VALOR HABRIA SI SE LE RESTA 1 A 0X00H 
    SUBWF   SEGUNDOS_TEMPORIZADOR_EDITAR,W
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_SEGUNDO_TEMPORIZADOR_EDITAR
    
    MOVLW   B'01011001' ;59 EN BCD
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    
    CORREGIR_BCD_SEGUNDO_TEMPORIZADOR_EDITAR
    MOVLW   B'00001111' 
    ANDWF   SEGUNDOS_TEMPORIZADOR_EDITAR,W;SE CONSERVA EL PRIMER DIGITO
    SUBLW   .15 ;ESTE VALOR DARIA SI PASA DE 0 A 9 
    BTFSS   STATUS,2 ;SI DA 1 SE SALTA LA INSTRUCCION (SE CORRIGE EL BCD)
    GOTO INCREMENTAR_SECOND
    
    MOVLW   .6 
    SUBWF   SEGUNDOS_TEMPORIZADOR_EDITAR,F ;SE COLOCA  EN 9 COMO DEBERIA DE SER
    
    INCREMENTAR_SECOND
    ;---------------------- SE VERIFICA SI SE DEBE INCREMENTAR -------------------------
    BTFSC   PORTB_ANTERIOR,1  ;SI EL ANTERIOR ERA 0 SE VERIFICA QUE EL SIGUIENTE SEA 1
    GOTO    SALIR_TEMPORIZADOR
    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR_TEMPORIZADOR
    
    ;----------- SI SE LLEGA HASTA AQUI ES QUE SE DEBIA INCREMENTAR ----------------------
    INCF    SEGUNDOS_TEMPORIZADOR_EDITAR,F
        ;SE VERIFICA SI MINUTOS_TEMPORIZADOR_EDITAR ES DIFERENCE DE CERO
    MOVF    MINUTOS_TEMPORIZADOR_EDITAR,W
    SUBLW   .0 ;SOLO SI ES 0 Z=1 
    BTFSS   STATUS,2 
    GOTO CORREGIR_BCD_TEMP_SEG_INC
    ;-------- SI MINUTOS ES CERO SE EJECUTA ESTO ----------------- 
    MOVF    SEGUNDOS_TEMPORIZADOR_EDITAR,W
    SUBLW   B'01011010' ;SI ESTO DA CERO
    BTFSS   STATUS,2
    GOTO    CORREGIR_BCD_TEMP_SEG_INC
    
    MOVLW   .1 ;59 EN BCD
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    CORREGIR_BCD_TEMP_SEG_INC
    ;----------------CORRECION DE BCD CUANDO SE PASA DE X9 A (X+1)0
    MOVLW   B'00001111' ;SE CONSERVA EL PRIMER DIGITO
    ANDWF   SEGUNDOS_TEMPORIZADOR_EDITAR,W
    SUBLW .10 ;SI DA 0 SE DEBE SUMAR LAS DECENAS
    BTFSS   STATUS,2 ;SI DA 0 SE SALTA ESTA INSTRUCCION
    GOTO VERIFICAR_OVERFLOW_SEGUNDOS_TEMPORIZADOR_EDITAR
    
    MOVLW   .6
    ADDWF   SEGUNDOS_TEMPORIZADOR_EDITAR,F ;SE ARREGLA EL BCD
    ;-----------------VERIFICAR SI SE PASA DE 59 A 00
    VERIFICAR_OVERFLOW_SEGUNDOS_TEMPORIZADOR_EDITAR
    MOVLW B'01100000' ;60 EN BCD
    SUBWF   SEGUNDOS_TEMPORIZADOR_EDITAR,W
    BTFSC   STATUS,2 ;SI DA 0 SE REINICIA
    CLRF    SEGUNDOS_TEMPORIZADOR_EDITAR
    
    GOTO SALIR_TEMPORIZADOR
    
    EDIT_MINUTE_TEMPORIZADOR
    ;------------------------ CADA VEZ QUE SE ENTRA AQUI SE VERIFICA SI SEGUNDOS ES 0 PARA ESTABLECER EL TIEMPO MINIMO----
    MOVLW .0
    SUBWF SEGUNDOS_TEMPORIZADOR_EDITAR,W
    BTFSS   STATUS,2 
    GOTO    VER_PORTB
    
    ;SI SEGUNDOS ES 0 ENTONCES SE VERIFICA QUE MINUTOS SEA 0 
    MOVLW .0
    SUBWF   MINUTOS_TEMPORIZADOR_EDITAR,W
    BTFSS   STATUS,2 
    GOTO VER_PORTB
    
    ;SI SE LLEGA HASTA AQUI ENTONCES ES QUE SE DEBE CORREGIR PARA NO DAR OPCION DE PONER 0 MIN 0 SEG
    MOVLW   .1
    MOVWF   SEGUNDOS_TEMPORIZADOR_EDITAR
    
    VER_PORTB
    ;------SE VERIFICA SI SE DEBE DECREMENTAR------------------
    BTFSC   PORTB_ANTERIOR,0 ;SE MIRA SI HUBO UN CAMBIO DE 0 A 1 (FLANCO DE SUBIDA
    GOTO    INCREMENTAR_MINUTE_TEMPORIZADOR
    
    BTFSS   PORTB_ACTUAL,0
    GOTO INCREMENTAR_MINUTE_TEMPORIZADOR
    ;--------SIm HUBO UN FLANCO DE SUBIDA SE EN RB0 SE EJECUTA ESTO ---------------
    DECF    MINUTOS_TEMPORIZADOR_EDITAR,F
    ;--- CORRECCION DEL BCD ----------
    ;PRIMERO SE VERIFICA SI SE PASO DE 00 A 99
    MOVLW .255 ;ESTE VALOR HABRIA SI SE LE RESTA 1 A 0X00H 
    SUBWF   MINUTOS_TEMPORIZADOR_EDITAR,W
    BTFSS   STATUS,2 ;SI ES 1 SE SALTA EL GOTO
    GOTO    CORREGIR_BCD_MINUTOS_TEMPORIZADOR_EDITAR
    
    MOVLW   B'10011001' ;99 EN BCD
    MOVWF   MINUTOS_TEMPORIZADOR_EDITAR
    
    CORREGIR_BCD_MINUTOS_TEMPORIZADOR_EDITAR
    MOVLW   B'00001111' 
    ANDWF   MINUTOS_TEMPORIZADOR_EDITAR,W;SE CONSERVA EL PRIMER DIGITO
    SUBLW   .15 ;ESTE VALOR DARIA SI PASA DE 0 A 9 
    BTFSS   STATUS,2 ;SI DA 1 SE SALTA LA INSTRUCCION
    GOTO INCREMENTAR_MINUTE_TEMPORIZADOR
    
    MOVLW   .6 
    SUBWF   MINUTOS_TEMPORIZADOR_EDITAR,F ;SE COLOCA  EN 9 COMO DEBERIA DE SER
    
    INCREMENTAR_MINUTE_TEMPORIZADOR
    ;---------------SE VERIFICA SI SE DEBE INCREMENTAR-----------------------------------------------
    BTFSC   PORTB_ANTERIOR,1
    GOTO    SALIR_TEMPORIZADOR
    
    BTFSS   PORTB_ACTUAL,1
    GOTO    SALIR_TEMPORIZADOR
    
    ;----------- SI SE LLEGA HASTA AQUI ES QUE SE DEBIA INCREMENTAR ----------------------
    INCF    MINUTOS_TEMPORIZADOR_EDITAR,F
    
    ;----------------CORRECION DE BCD CUANDO SE PASA DE X9 A (X+1)0
    MOVLW   B'00001111' ;SE CONSERVA EL PRIMER DIGITO
    ANDWF   MINUTOS_TEMPORIZADOR_EDITAR,W
    SUBLW .10 ;SI DA 0 SE DEBE SUMAR LAS DECENAS
    BTFSS   STATUS,2 ;SI DA 0 SE SALTA ESTA INSTRUCCION
    GOTO VERIFICAR_OVERFLOW_MINUTOS_EDITAR
    
    MOVLW   .6
    ADDWF  MINUTOS_TEMPORIZADOR_EDITAR,F ;SE ARREGLA EL BCD
    ;-----------------VERIFICAR SI SE PASA DE 99 A 00
    VERIFICAR_OVERFLOW_MINUTOS_EDITAR
    MOVLW B'10100000' 
    SUBWF   MINUTOS_TEMPORIZADOR_EDITAR,W
    BTFSC   STATUS,2 ;SI DA 0 SE REINICIA
    CLRF    MINUTOS_TEMPORIZADOR_EDITAR
    ;------- LUEGO DE TODO SE LLEGA A ESTE PUNTO --------------------------------------------------
    SALIR_TEMPORIZADOR
    ;------- SE VERIFICA QUE SI SE HA SALIDO SOLO PULSANDO EL BOTON (SALIDA SIN GUARDAR DATOS)--------
    BTFSC   PORTB_ANTERIOR,5
    RETURN
    
    BTFSS   PORTB_ACTUAL,5
    RETURN
    
  
    ;-------------- SI HUBO FLANCO DE SUBIDA SE LLEGA A ESTE PUNTO -----------------------------------
    BCF FLAGS,3 ;SE DESHABILITA EL BIT DE VERIFICACION DE PRIMERA VEZ ENTRANDO A EDITAR HORA
    BCF FLAGS,2 ;SE COLOCA EN EDITAR MINUTO/DIA 
    BCF FLAGS,1 ;SE DESHABILITA EL MODO DE EDICION
    
   MOVLW   VALOR_TMR1H
   MOVWF   TMR1H
   MOVLW   VALOR_TMR1L
    MOVWF   TMR1L
    
    MOVLW   VALOR_TMR1
    MOVWF   CONTADOR_TMR1
    BCF	T1CON,0 ;SE APAGA EL TMR1
    
    RETURN 
    
    
DELAY_US: ;RETARDO DE 228uS
    MOVLW   .255
    MOVWF   DELAY
    DECFSZ  DELAY, F
    GOTO $-1
    RETURN
    END